SPEC java.security.AlgorithmParameters
OBJECTS
    java.lang.String alg;
    java.security.spec.AlgorithmParameterSpec params;
	byte[] parAr; 
	java.lang.String format;
	byte[] parsRes;

EVENTS
    g1: getInstance(alg);
    g2: getInstance(alg, _);
    Gets := g1 | g2;

    i1: init(params);
    i2: init(parAr);
    i3: init(parAr, _);
    Inits := i1 | i2 | i3;
    
    e1: parsRes = getEncoded();
    e2: parsRes = getEncoded(format);
    Encoded := e1 | e2;

ORDER
    Gets, Inits, Encoded?

CONSTRAINTS
	alg in {"AES", "Blowfish", "Camellia", "ChaCha7539", "DESede", "DSA", "DiffieHellman", "DH", "EC", "ElGamal", "Rijndael", "Salsa20", "Tnepres", 
			"Serpent", "SHACAL-2", "Twofish", "XSalsa20", "GCM", "CCM"};

REQUIRES    
    preparedAlg[parAr];
    alg in {"AES", "Blowfish", "Twofish", "Camellia", "ChaCha7539", "DESede", "Rijndael", "Salsa20", "Tnepres", "Serpent", "SHACAL-2", "XSalsa20"} => preparedIV[params];
    alg in {"DiffieHellman", "DH"} => preparedDH[params];
    alg in {"DSA"} => preparedDSA[params];
    alg in {"EC"} => preparedEC[params];
    
ENSURES 
    preparedAlg[this, alg] after Inits;
    preparedAlg[parsRes, alg] after Encoded;