SPEC javax.crypto.SecretKeyFactory
OBJECTS
    java.lang.String keyFactoryAlgorithm;
    javax.crypto.SecretKey key;
    javax.crypto.SecretKey otherKey;
    java.security.spec.KeySpec keySpec;
    int keylength;
   
EVENTS
    g1: getInstance(keyFactoryAlgorithm);
    g2: getInstance(keyFactoryAlgorithm, _);
    Gets := g1 | g2;

    gS: key = generateSecret(keySpec);
    tK: key = translateKey(otherKey);
    
    Gens := gS | tK;

ORDER
    Gets, Gens

CONSTRAINTS
	keyFactoryAlgorithm in {"AES", "PBKDF2WithHmacSHA224", "PBKDF2WithHmacSHA256", "PBKDF2WithHmacSHA384", "PBKDF2WithHmacSHA512", 
							"PBEWithSHA256And192BitAES-CBC-BC", "PBEWithSHA256And256BitAES-CBC-BC", "Camellia", "DESede", "PBEwithSHAandTwofish-CBC"};
	keyFactoryAlgorithm in {"AES", "PBKDF2WithHmacSHA224", "PBKDF2WithHmacSHA256", "PBKDF2WithHmacSHA384", "PBKDF2WithHmacSHA512"} => keylength in {128, 192, 256};

REQUIRES
	speccedKey[keySpec, _];
ENSURES
    generatedKey[key, keyFactoryAlgorithm];