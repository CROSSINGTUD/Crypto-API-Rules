SPEC java.security.KeyStore

OBJECTS
	java.io.InputStream fileinput;
    
	char[] passwordIn;
	char[] passwordOut;
	char[] passwordKey;
    
	java.security.KeyStore.Entry entry;
	byte[] byteKey;
    
	java.security.KeyStore.LoadStoreParameter paramLoad;
	java.security.KeyStore.ProtectionParameter protParamGet;
	java.security.KeyStore.ProtectionParameter protParamSet;
	java.lang.String aliasGet;
	java.lang.String aliasSet;
	java.io.OutputStream fileoutput;
	java.security.KeyStore.LoadStoreParameter paramStore;
	java.lang.String keyStoreAlgorithm;
	java.security.cert.Certificate[] chain;
	java.security.cert.Certificate cert;
    
	java.security.Key key;
	java.lang.String alias;
    
EVENTS
	g1: getInstance(keyStoreAlgorithm);
	g2: getInstance(keyStoreAlgorithm, _);
	Gets := g1 | g2;

	l1: load(fileinput, passwordIn);
	l2: load(paramLoad);
	Loads := l1 | l2;

	s1:store(paramStore);
	s2:store(fileoutput, passwordOut);
	Stores := s1 | s2;

	GetEntry: getEntry(aliasGet, protParamGet);
    
	SetEntry: setEntry(aliasSet, entry, protParamSet);
        
	GetKey: key = getKey(alias, passwordKey);

ORDER
	Gets, Loads, ((GetEntry?, GetKey) | (SetEntry, Stores))*

CONSTRAINTS
	keyStoreAlgorithm in {"JCEKS", "JKS", "DKS", "PKCS11", "PKCS12"}; 
	neverTypeOf[passwordIn, java.lang.String];
	neverTypeOf[passwordOut, java.lang.String];
	neverTypeOf[passwordKey, java.lang.String];
	notHardCoded[passwordIn];

ENSURES
	generatedKeyStore[this] after Loads;
	generatedKey[key, _];
	generatedPrivkey[key];
	generatedPubkey[key];
	