SPEC java.security.MessageDigest
USES_OBJECTS
    java.lang.String alg;
    byte pre_inbyte;
    byte[] pre_inbytearr;
    int pre_off;
    int pre_len;
    java.nio.ByteBuffer pre_inpBuf;
    byte[] inbytearr;
    int off;
    int len;
    byte[] out;
    
    byte[] comp1;
    byte[] comp2;

FORBIDDEN_EVENTS
	equals(java_lang_Object) => iE;
	
REQUIRED_EVENTS
    g1: getInstance(alg);
    g2: getInstance(alg, _);
    Gets := g1, g2;

    u1: update(pre_inbyte);
    u2: update(pre_inbytearr);
    u3: update(pre_inbytearr, pre_off, pre_len);
    u4: update(pre_inpBuf);
    Updates := u1, u2, u3, u4;

    d1: out = digest();
    d2: out = digest(inbytearr);
    d3: digest(out, off, len);
    DWOU := d2;
    DWU := d1, d3;
    Digests := DWU, DWOU;
    r: reset();
    
    iE: isEqual(comp1, comp2);

ENFORCE_ORDER
    (Gets | r), (DWOU | (Updates+, Digests)) 

ENFORCES_CONSTRAINTS
    alg in {"SHA-256", "SHA-384", "SHA-512"}
    pre_len > pre_off
    len > off

ENSURES
    digested(inbytearr, out);