/*
 * This is a CrySL specification for the Google Tink implementation of 
 * the class MacKeyTemplates. 
 * 
 * Note: in this case, we are not specifying the correct usage of the 
 * API, but actually guaranteeing that the Google Tink API implementation 
 * actually exposes some key templates as static attributes. This 
 * information simplifies the specification of other properties for 
 * correctly using the API. 
 * 
 * It constraints the use of the API to the following keys: 
 * 
 * - hmac_sha256_128bittag
 * - hmac_sha256_256bittag
 */

SPEC com.google.crypto.tink.mac.MacKeyTemplates

OBJECTS
	com.google.crypto.tink.proto.KeyTemplate hmac_sha256_128bittag;
	com.google.crypto.tink.proto.KeyTemplate hmac_sha256_256bittag;
	
	int k16;
	int k32; 
	
	int hashType;  // HashType enum: (1) - SHA1, (3) - SHA256, and (4) - SHA 512. 
	
EVENTS
	hmac_sha256_128bittag_evt : hmac_sha256_128bittag = createHmacKeyTemplate(k32, k16, _);
	hmac_sha256_256bittag_evt : hmac_sha256_256bittag = createHmacKeyTemplate(k32, k32, _);
	
	
	CreateKeyTemplates := hmac_sha256_128bittag_evt | hmac_sha256_256bittag_evt;
	
ORDER 
	(CreateKeyTemplates)

CONSTRAINTS
	k16 == 16;
	k32 == 32; 
	
	//hashType == 3; 
	
	
ENSURES
	macKeyTemplate[hmac_sha256_128bittag] after hmac_sha256_128bittag_evt;
	macKeyTemplate[hmac_sha256_256bittag] after hmac_sha256_256bittag_evt;