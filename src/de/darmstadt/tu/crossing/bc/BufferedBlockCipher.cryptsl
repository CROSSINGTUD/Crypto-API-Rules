SPEC org.bouncycastle.crypto.BufferedBlockCipher

OBJECTS
	org.bouncycastle.crypto.BlockCipher engineOrMode;
	org.bouncycastle.crypto.CipherParameters params;
	
	int pre_plain_off;
	int pre_ciphertext_off;
	int ciphertext_off;
	
	int pre_len;
	boolean is_encryption;
	
	byte pre_plain;
	byte[] pre_plaintext;
	byte[] pre_ciphertext;
	
	byte[] cipherText;
	
EVENTS
	c1 : BufferedBlockCipher(engineOrMode);
	Cons := c1;

	i : init(is_encryption, params);
	Inits := i;

//	@return the number of output bytes copied to out.
	p1: processByte(pre_plain, pre_ciphertext, pre_ciphertext_off);
	p2: processBytes(pre_plaintext, pre_plain_off, pre_len, pre_ciphertext, pre_ciphertext_off);
	Procs := p1 | p2;
	
//	@return the number of output bytes copied to out.
	f : doFinal(cipherText, ciphertext_off);
	DOFINALS := f;
	
ORDER
	Cons, (Inits, Procs, DOFINALS)+
	
CONSTRAINTS
	length(pre_plaintext) >= pre_plain_off + pre_len;
	length(pre_ciphertext) <= pre_ciphertext_off;
	length(cipherText) <= ciphertext_off;
	
REQUIRES
	generatedEngineOrMode[engineOrMode];
	generatedCipherParams[params];
//	preparedIV[params]; //this is added for BlockCipherVectorTest & BlockCipherMonteCarloTest
	
ENSURES
	encrypted[pre_ciphertext, pre_plaintext] after Procs;
	encrypted[cipherText];
	generatedCipher[this];