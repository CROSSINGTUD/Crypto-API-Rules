SPEC org.bouncycastle.crypto.macs.CBCBlockCipherMac

OBJECTS
	org.bouncycastle.crypto.BlockCipher cipher;
	org.bouncycastle.crypto.CipherParameters params;
	org.bouncycastle.crypto.paddings.BlockCipherPadding padding;
	byte[] inputArray;
	byte[] outputArray;
	byte input;
	int inputOff;
	int outputOff;
	int len;
	int macSizeInBits;
	
EVENTS
	c1 : CBCBlockCipherMac(cipher);
	c2 : CBCBlockCipherMac(cipher, padding);
	c3 : CBCBlockCipherMac(cipher, macSizeInBits);
	c4 : CBCBlockCipherMac(cipher, macSizeInBits, padding);
	Cons := c1 | c2 | c3 | c4;
	
	i : init(params);
	Inits := i;
	
	u1 : update(input);
	u2 : update(inputArray, inputOff, len);
	Updates := u1 | u2;
	
	f : doFinal(outputArray, outputOff);
	Finals := f;
	
ORDER
	Cons, Inits, (Finals | (Updates+, Finals))
	
CONSTRAINTS
	len >= 0;
//	macSizeInBits % 8 == 0;
	
REQUIRES
	generatedPadding[padding]; // padding can also be null
	generatedKey[params];
	generatedEngine[cipher];
	
ENSURES
	generatedMac[this];
	
