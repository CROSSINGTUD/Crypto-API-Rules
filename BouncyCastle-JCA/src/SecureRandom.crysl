SPEC java.security.SecureRandom

OBJECTS 
	byte[] seed;
	byte[] genSeed;
	java.lang.String randomAlgorithm;
	long lSeed;
	byte[] next;
	int numB;
	int randInt;

EVENTS
	c1: SecureRandom();
	c2: SecureRandom(seed);
	Cons := c1 | c2;
	
	g1: getInstance(randomAlgorithm);
	g2: getInstance(randomAlgorithm, _);
	gI: getInstanceStrong();
	Gets := g1 | g2 | gI;
	
	Ins := Gets | Cons;

	gS: genSeed = generateSeed(_);	

	s1: setSeed(seed);
	s2: setSeed(lSeed);
	Seeds := s1 | s2;
	
	nB: nextBytes(next);
	nI: randInt = nextInt();
	Nexts := nB | nI;
	
	Ends := gS | Nexts;

ORDER
	Ins, (Seeds?, Ends*)*
 	
CONSTRAINTS
	randomAlgorithm in {"DEFAULT", "NONCEANDIV"};
	
REQUIRES	
	randomized[seed];
	randomized[lSeed];
	
ENSURES
	randomized[this] after Ins;
	randomized[genSeed] after gS;
	randomized[next] after nB;
	randomized[numB] after ne;
	randomized[randInt] after nI;
	