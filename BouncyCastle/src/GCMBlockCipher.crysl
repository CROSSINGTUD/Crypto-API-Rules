SPEC org.bouncycastle.crypto.modes.GCMBlockCipher

OBJECTS
	org.bouncycastle.crypto.BlockCipher engine;
	org.bouncycastle.crypto.CipherParameters params;
	
	byte aadByte;
	byte[] aadBytes;
	int aadOff;
	int aadLen;
	
	byte plainTextByte;
	byte[] plainText;
	int plainTextOffset;
	int plainTextLen;
	
	byte[] cipherText;
	int cipherTextOffset;
	
	boolean isEncryption;
	
EVENTS
	c1: GCMBlockCipher(engine);
	Con := c1;
	
	i1: init(isEncryption, params);
	Init := i1;
	
	pa1: processAADByte(aadByte);
	pa2: processAADBytes(aadBytes, aadOff, aadLen);
	ProcessAAD := pa1 | pa2;
	
	p1: processByte(plainTextByte, cipherText, cipherTextOffset);
	p2: processBytes(plainText, plainTextOffset, plainTextLen, cipherText, cipherTextOffset);
	Process := p1 | p2;
	
	df1: doFinal(cipherText, cipherTextOffset);
	DoFinal := df1;
	
ORDER
	Con, Init, ProcessAAD*, Process+, DoFinal
	
CONSTRAINTS
	neverTypeOf[engine, org.bouncycastle.crypto.engines.AESFastEngine];
	length[aadBytes] >= aadOff + aadLen;
	length[plainText] >= plainTextOffset + plainTextLen;
	length[cipherText] >= cipherTextOffset;
	plainTextOffset >= 0;
	plainTextLen > 0;
	cipherTextOffset >= 0;

REQUIRES
	generatedAESEngine[engine] || generatedAESLightEngine[engine];
	generatedAEADParameters[params] || generatedParametersWithIV[params];
	
ENSURES
	generatedGCMBlockCipherMode[this] after Con;

